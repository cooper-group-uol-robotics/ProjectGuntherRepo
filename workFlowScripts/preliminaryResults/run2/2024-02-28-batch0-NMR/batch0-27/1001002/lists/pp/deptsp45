;deptsp45
;avance-version (14/10/09)
;dept polarization transfer
;with decoupling during acquisition
;using shaped pulse for 180degree pulse on f1 - channel
;with 45 degree read pulse to give all signals positive
;
;$CLASS=HighRes
;$DIM=1D
;$TYPE=
;$SUBTYPE=
;$COMMENT=


#include <Avance.incl>
#include <Delay.incl>


"p4=p3*2"
"d2=1s/(cnst2*2)"
"d12=20u"


"DELTA=p1*4/PI"


"acqt0=-p1*2/PI"


1 ze
2 30m do:f2 
  d1
  d12 pl1:f1 pl2:f2
  (p3 ph1):f2
  d2
  (p4 ph2):f2 
  (p1 ph4):f1
  d2 pl0:f1
  (p3*0.5 ph3):f2 (p13:sp5 ph5):f1
  d2
  DELTA pl12:f2
  go=2 ph31 cpd2:f2
  30m do:f2 mc #0 to 2 F0(zd)
exit


ph1=0
ph2=0 2 1 3
ph3=1 1 1 1 3 3 3 3
ph4=0 0 0 0 0 0 0 0 1 1 1 1 1 1 1 1
    2 2 2 2 2 2 2 2 3 3 3 3 3 3 3 3
ph5=0 2 0 2 0 2 0 2 1 3 1 3 1 3 1 3
ph31=1 1 3 3 3 3 1 1 2 2 0 0 0 0 2 2
     3 3 1 1 1 1 3 3 0 0 2 2 2 2 0 0


;pl0 : 0W
;pl1 : f1 channel - power level for pulse (default)
;pl2 : f2 channel - power level for pulse (default)
;pl12: f2 channel - power level for CPD/BB decoupling
;sp5: f1 channel - shaped pulse (180degree)
;spnam5: Crp60comp.4
;p1 : f1 channel -  90 degree high power pulse
;p3 : f2 channel -  90 degree high power pulse
;p4 : f2 channel - 180 degree high power pulse
;p13: f1 channel - 180 degree shaped pulse
;     = 2msec for Crp60comp.4
;d1 : relaxation delay; 1-5 * T1
;d2 : 1/(2J(XH))
;d12: delay for power switching                      [20 usec]
;cnst2: = J(XH)
;ns: 4 * n, total number of scans: NS * TD0
;ds: 8
;cpd2: decoupling according to sequence defined by cpdprg2
;pcpd2: f2 channel - 90 degree pulse for decoupling sequence


;DELTA: delay to compensate for chemical shift evolution during
;          RF pulse in order to give same phase correction as
;          for reference 1D spectrum



;$Id:$
